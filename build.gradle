apply plugin: 'java'

description = 'Hash array mapped trie field mapper plugin for ElasticSearch'
version = '0.1'

ext {
    elasticsearchVersion = '2.0.0'
    luceneVersion = '5.2.1'

    pluginName = 'hamt-field-mapper'
    pluginClassname = 'org.elasticsearch.plugin.mapper.MapperHamtPlugin'
    pluginMetadataDir = 'plugin-metadata'
    pluginMetadataProperties = 'plugin-descriptor.properties'
    dependenciesDir = "${buildDir}/dependencies"
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':hamt')
    compile "org.elasticsearch:elasticsearch:$elasticsearchVersion"
    testCompile('org.hamcrest:hamcrest-all:1.3') {
        exclude module: 'hamcrest-core'
    }
    testCompile('junit:junit:4.11') {
        exclude module: 'hamcrest-core'
    }
    testCompile('com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.1.16') {
        exclude module: 'junit'
    }
    testCompile "org.elasticsearch:elasticsearch:$elasticsearchVersion:tests"
    testCompile("org.apache.lucene:lucene-test-framework:$luceneVersion")  {
        exclude module: 'junit'
    }
}

task copyDependencies(type: Copy) {
    from configurations.runtime
    into dependenciesDir
    include 'hamt-*.jar'
}

task processPluginMetadata(type: Copy) {
    def tokens = [
        pluginName: pluginName,
        pluginClassname: pluginClassname,
        description: project.description,
        version: project.version,
        elasticsearchVersion: elasticsearchVersion
    ]
    def engine = new groovy.text.GStringTemplateEngine()

    from "${pluginMetadataDir}/${pluginMetadataProperties}"
    into "${buildDir}/${pluginMetadataDir}/"
    filter {
        engine.createTemplate(it).make(tokens).toString()
    }
}
processPluginMetadata.outputs.upToDateWhen { false }

task buildZip(type: Zip) {
    from jar
    from dependenciesDir
    from "${buildDir}/${pluginMetadataDir}/${pluginMetadataProperties}"
}

build.dependsOn buildZip
buildZip.dependsOn copyDependencies
buildZip.dependsOn processPluginMetadata
