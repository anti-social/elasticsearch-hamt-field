apply plugin: 'java'

description = 'Hash array mapped trie field mapper plugin for ElasticSearch'
version = '0.1'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"
}

ext {
    defaultElasticsearchVersion = '2.1.0'
    elasticsearchVersion = project.hasProperty('esVersion') ? project.getProperty('esVersion') : defaultElasticsearchVersion
    luceneVersion = '5.3.1'

    pluginName = 'htable-field-mapper'
    pluginClassname = 'org.elasticsearch.plugin.mapper.MapperHtablePlugin'
    dependenciesDir = "${buildDir}/dependencies"
    pluginMetadataFilename = 'plugin-descriptor.properties'
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':htable')
    compile "org.elasticsearch:elasticsearch:$elasticsearchVersion"
    testCompile('org.hamcrest:hamcrest-all:1.3') {
        exclude module: 'hamcrest-core'
    }
    testCompile('junit:junit:4.11') {
        exclude module: 'hamcrest-core'
    }
    testCompile('com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.1.16') {
        exclude module: 'junit'
    }
    testCompile "org.elasticsearch:elasticsearch:$elasticsearchVersion:tests"
    testCompile("org.apache.lucene:lucene-test-framework:$luceneVersion")  {
        exclude module: 'junit'
    }
}

task copyDependencies(type: Copy) {
    from configurations.runtime
    into dependenciesDir
    include 'htable-*.jar'
}

task processPluginMetadata << {
    def pluginMetadataFile = new File("${buildDir}/${pluginMetadataFilename}")
    pluginMetadataFile.text = """
name=${pluginName}
description=${project.description}
version=${project.version}
classname=${pluginClassname}
site=false
jvm=true
java.version=${sourceCompatibility}
elasticsearch.version=${elasticsearchVersion}
"""
}
processPluginMetadata.outputs.upToDateWhen { false }

task buildZip(type: Zip) {
    from jar
    from dependenciesDir
    from "${buildDir}/${pluginMetadataFilename}"
}

build.dependsOn buildZip
buildZip.dependsOn copyDependencies
buildZip.dependsOn processPluginMetadata
